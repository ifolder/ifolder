AC_INIT(src/iFolder/iFolder.cs)
AC_CANONICAL_SYSTEM
AM_CONFIG_HEADER(config.h)

PASSALONG_CONFIG_OPTIONS=$*

AM_INIT_AUTOMAKE(ifolder, 0.8)
VERSION=$VERSION.`date +%Y%m%d`
AM_MAINTAINER_MODE
AC_PROG_CC

#
# Check for a valid operating system and C# compiler
#
case $host_os in
    linux*)
        IFOLDER_OS='linux'
        AC_CHECK_PROG(CSC, mcs, mcs)
        test -z "$CSC" && AC_MSG_ERROR([no C Sharp compiler: mcs not found in \$PATH])
    ;;
    cygwin*)
        IFOLDER_OS='windows'
        AC_CHECK_PROG(CSC, csc, csc)
        test -z "$CSC" && AC_CHECK_PROG(CSC, mcs, mcs)
        test -z "$CSC" && AC_MSG_ERROR([no C Sharp compiler: neither csc nor mcs found in \$PATH])
    ;;
    *)
        AC_MSG_ERROR([Unknown host_os: $host_os])
    ;;
esac
AC_SUBST(IFOLDER_OS)
AM_CONDITIONAL(LINUX, test "$IFOLDER_OS" = "linux")
AM_CONDITIONAL(WINDOWS, test "$IFOLDER_OS" = "windows")

#
# Configure C# compiler settings
#
case $CSC in
    #
    # Mono-specific configuration
    #
    mcs)
        CSC_EXEFLAG=/target:exe
        CSC_LIBFLAG=/target:library
        CSC_WINEXEFLAG=/target:winexe
        CSCFLAGS='/d:MONO /warn:4 /d:TRACE'
        CSCFLAGS_DEBUG="/debug+ /d:DEBUG"
        CSCFLAGS_OPTIMIZE="/optimize+"
        MONO=mono
        MONO_DEBUG='mono --debug'
        MONO_PATH='$(subst $(SPACE),:,$(addprefix $(SRCDIR)$(SEP),$(COMMON_SUBDIRS))):$(ADDRESSBOOK_INSTALL_PATH)/bin:$(SIMIAS_INSTALL_PATH)/bin'
        SYSTEM_XML='System.Xml.dll'
    ;;
    #
    # .NET-specific configuration
    #
    csc)
        CSC_EXEFLAG=/target:exe
        CSC_LIBFLAG=/target:library
        CSC_WINEXEFLAG=/target:winexe
        CSCFLAGS='/d:DOTNET /warn:4 /d:TRACE /nologo'
        CSCFLAGS_DEBUG="/debug+ /d:DEBUG"
        CSCFLAGS_OPTIMIZE="/optimize+"
        MONO=
        MONO_DEBUG=
        MONO_PATH=
        SYSTEM_XML='System.XML.dll'
    ;;
esac
AC_SUBST(CSC)
AC_SUBST(CSC_EXEFLAG)
AC_SUBST(CSC_LIBFLAG)
AC_SUBST(CSC_WINEXEFLAG)
AC_SUBST(CSCFLAGS)
AC_SUBST(MONO)
AC_SUBST(MONO_PATH)
AC_SUBST(SYSTEM_XML)

SRCDIR='$(top_srcdir)/src'
TOOLDIR='$(top_srcdir)/tools'
DOCDIR="$SRCDIR/doc"

AC_SUBST(SRCDIR)
AC_SUBST(TOOLDIR)
AC_SUBST(DOCDIR)

EMPTY=
SPACE='$(EMPTY) $(EMPTY)'

AC_SUBST(EMPTY)
AC_SUBST(SPACE)

COMMON_SUBDIRS="iFolder"
LINUX_SUBDIRS="TrayIcon GtkTrayApp"
WINDOWS_SUBDIRS="FormsTrayApp FormsInvitationWizard iFolderShell"
NOBUILD_SUBDIRS="iFolderNautilus"

AC_SUBST(COMMON_SUBDIRS)
AC_SUBST(LINUX_SUBDIRS)
AC_SUBST(WINDOWS_SUBDIRS)
AC_SUBST(NOBUILD_SUBDIRS)

#
# Set platform-specific variables
#
case $IFOLDER_OS in
    #
    # Linux-specific configuration
    #
    linux)
        #
        # Set variables
        #
        COMMON_CLEAN_FILES='.cstore .simias'
        EMPTY=
        EXE_EXT=''
        ICON_EXT='.ico'
        ICON_FLAG='/resource:'
        NUNIT="$TOOLDIR/NUnit/mono/bin/nunit-console.exe"
        NUNIT_LIBPATH="$TOOLDIR/mono/NUnit/bin"
        NUNIT_FLAGS='/nologo'
        NUNITFLAGS_DEBUG=''
        NUNITFLAGS_OPTIMIZE=''
        OBJ_EXT='.o'
        PLATFORM_SUBDIRS=$LINUX_SUBDIRS
        REPORT_DIR='$(DESTDIR)$(bindir)'
        REPORT_EXE='mono $(top_srcdir)/tools/Report/Report.exe'
        SEP='/'
        SHARED_LIB_EXT='.so'
        SHARED_LIB_FLAG='-shared'
        SQLITELIB_PATH="../../external/sqlite/linux"
        SQLITELIB="libsqlite.so"
        SYSTEM_XML='System.Xml.dll'
        #
        # Set pkg-config variables
        #
        MONO_MINIMUM_VERSION=0.91
        AC_SUBST(MONO_MINIMUM_VERSION)
        
        GTK_SHARP_MINIMUM_VERSION=0.17
        AC_SUBST(GTK_SHARP_MINIMUM_VERSION)
	
	GDK20_MINIMUM_VERSION=2.2.3
	ATK_MINIMUM_VERSION=1.2.4
        
        PKG_CONFIG_REQUIRES="mono-core >= $MONO_MINIMUM_VERSION, gtk-sharp >= $GTK_SHARP_MINIMUM_VERSION, gdk-2.0 >= $GDK20_MINIMUM_VERSION, atk >= $ATK_MINIMUM_VERSION"
        AC_SUBST(PKG_CONFIG_REQUIRES)
        
        PKG_CONFIG_CONFLICTS=""
        AC_SUBST(PKG_CONFIG_CONFLICTS)
    ;;
    #
    # Windows-specific configuration
    #
    windows)
        #
        # Check for valid compilers
        #
        AC_CHECK_PROG(CC, cl, cl)
        test -z "$CC" && AC_MSG_ERROR([no acceptable C compiler found in \$PATH])
        AC_CHECK_PROG(CXX, cl, cl)
        test -z "$CXX" && AC_MSG_ERROR([no acceptable C++ compiler found in \$PATH])
        AC_CHECK_PROG(LD, link, link)
        test -z "$LD" && AC_MSG_ERROR([no acceptable linker found in \$PATH])
        #
        # Set variables
        #
        CC='cl -c'
        CXX='cl -c'
        CDEF='-D'
        CFLAGS="-nologo -W3 -DWIN32 -D_WINDOWS -DWINDOWS -DPACKAGE=\$(PACKAGE) -DVERSION=\$(VERSION)";
        CFLAGS_DEBUG="-MDd -Od -Zi -RTC1 -DDEBUG -D_DEBUG"
        CFLAGS_OPTIMIZE="-MD -O2 -DNDEBUG"
        CINC='-I'
        COMMON_CLEAN_FILES='*.suo */*.suo *.csproj.user */*.csproj.user bin obj */bin */obj *.xml */*.xml *.pdb */*.pdb .cstore .simias'
        COUT='-Fo'
        CXXFLAGS="-nologo -GX -W3 -DWIN32 -D_WINDOWS -DWINDOWS -DPACKAGE=\$(PACKAGE) -DVERSION=\$(VERSION)";
        CXXFLAGS_DEBUG="-MDd -Od -Zi -RTC1 -DDEBUG -D_DEBUG"
        CXXFLAGS_OPTIMIZE="-MD -O2 -DNDEBUG"
        DEF='-def:'
        EMPTY=
        EXE_EXT='.exe'
        ICON_EXT='.ico'
        ICON_FLAG='/win32icon:'
        LDFLAGS='-nologo -machine:X86'
        LDFLAGS_DEBUG="-DEBUG"
        LDINC='-libpath:'
        LDOUT='-out:'
        LIB_EXT='.lib'
        LIB_PRE=''
        LIBFLAG=''
        LIBS='kernel32.lib user32.lib gdi32.lib winspool.lib comdlg32.lib advapi32.lib shell32.lib ole32.lib oleaut32.lib uuid.lib odbc32.lib odbccp32.lib rpcrt4.lib ws2_32.lib imagehlp.lib'
        NUNIT="$TOOLDIR/NUnit/net/bin/nunit-console.exe"
        NUNIT_LIBPATH="$TOOLDIR/NUnit/net/bin"
        NUNIT_FLAGS='/nologo'
        NUNITFLAGS_DEBUG=''
        NUNITFLAGS_OPTIMIZE=''
        OBJ_EXT='.obj'
        PLATFORM_SUBDIRS=$WINDOWS_SUBDIRS
        RC='rc'
        RC_FLAGS='-r'
        REPORT_DIR='$(shell cygpath --windows $(DESTDIR)$(bindir) | sed ''s,\\\\,/,g'')'
        REPORT_EXE='$(top_srcdir)/tools/Report/Report.exe'
        RES_EXT='.res'
        SEP='$(EMPTY)\\$(EMPTY)'
        SHARED_LIB_EXT='.dll'
        SHARED_LIB_FLAG='-dll'
        SQLITELIB="sqlite.dll"
        SQLITELIB_PATH="../../external/sqlite/w32"
        SYSTEM_XML='System.XML.dll'
        TLBX='tlbexp'
        TYPE_LIB_EXT='.tlb'
    ;;
esac
AC_SUBST(CDEF)
AC_SUBST(CINC)
AC_SUBST(COMMON_CLEAN_FILES)
AC_SUBST(COUT)
AC_SUBST(CXX)
AC_SUBST(CXXFLAGS)
AC_SUBST(DEF)
AC_SUBST(DOCDIR)
AC_SUBST(ICON_EXT)
AC_SUBST(ICON_FLAG)
AC_SUBST(LDFLAGS)
AC_SUBST(LDINC)
AC_SUBST(LDOUT)
AC_SUBST(LIBS)
AC_SUBST(NUNIT)
AC_SUBST(NUNIT_FLAGS)
AC_SUBST(NUNIT_LIBPATH)
AC_SUBST(OBJ_EXT)
AC_SUBST(PLATFORM_SUBDIRS)
AC_SUBST(RC)
AC_SUBST(RCFLAGS)
AC_SUBST(REPORT_DIR)
AC_SUBST(REPORT_EXE)
AC_SUBST(RES_EXT)
AC_SUBST(SEP)
AC_SUBST(SHARED_LIB_FLAG)
AC_SUBST(SYSTEM_XML)
AC_SUBST(TLBX)

#
# Run standard macros
#
AM_PROG_CC_STDC
AC_PROG_INSTALL
AC_HEADER_STDC
AM_PROG_LIBTOOL

#
# Handle --with-simias-install-path
#
AC_ARG_WITH(simias-install-path, [
  --with-simias-install-path=DIR
                          path to where Simias is installed [[PREFIX]]],
    [ SIMIAS_INSTALL_PATH=$withval ],
    [ SIMIAS_INSTALL_PATH=$prefix ]
    )
test "x$SIMIAS_INSTALL_PATH" = xNONE && SIMIAS_INSTALL_PATH=$ac_default_prefix
mkdir -p $SIMIAS_INSTALL_PATH || AC_MSG_ERROR([mkdir -p $SIMIAS_INSTALL_PATH failed])
SIMIAS_INSTALL_PATH=`cd $SIMIAS_INSTALL_PATH && pwd`
case $IFOLDER_OS in
    windows)
        SIMIAS_INSTALL_PATH=`cygpath --windows $SIMIAS_INSTALL_PATH | sed 's,\\\\,/,g'`
    ;;
esac
echo SIMIAS_INSTALL_PATH=$SIMIAS_INSTALL_PATH
AC_SUBST(SIMIAS_INSTALL_PATH)

#
# Handle --with-simias-build-path
#
AC_ARG_WITH(simias-build-path, [
  --with-simias-build-path=DIR
                          path to a Simias build [[NONE]]],
    [ SIMIAS_BUILD_PATH=$withval ],
    [ SIMIAS_BUILD_PATH=NONE ]
    )
if test "x$SIMIAS_BUILD_PATH" = xNONE; then
    SIMIAS_BUILD_PATH=''
else
    test -d $SIMIAS_BUILD_PATH || AC_MSG_ERROR([$SIMIAS_BUILD_PATH does not exist])
    SIMIAS_BUILD_PATH=`cd $SIMIAS_BUILD_PATH && pwd`
    case $IFOLDER_OS in
        windows)
            SIMIAS_BUILD_PATH=`cygpath --windows $SIMIAS_BUILD_PATH | sed 's,\\\\,/,g'`
        ;;
    esac
fi
echo SIMIAS_BUILD_PATH=$SIMIAS_BUILD_PATH
AC_SUBST(SIMIAS_BUILD_PATH)

#
# Handle --with-addressbook-install-path
#
AC_ARG_WITH(addressbook-install-path, [
  --with-addressbook-install-path=DIR
                          path to where AddressBook is installed [[PREFIX]]],
    [ ADDRESSBOOK_INSTALL_PATH=$withval ],
    [ ADDRESSBOOK_INSTALL_PATH=$prefix ]
    )
test "x$ADDRESSBOOK_INSTALL_PATH" = xNONE && ADDRESSBOOK_INSTALL_PATH=$ac_default_prefix
mkdir -p $ADDRESSBOOK_INSTALL_PATH || AC_MSG_ERROR([mkdir -p $ADDRESSBOOK_INSTALL_PATH failed])
ADDRESSBOOK_INSTALL_PATH=`cd $ADDRESSBOOK_INSTALL_PATH && pwd`
case $IFOLDER_OS in
    windows)
        ADDRESSBOOK_INSTALL_PATH=`cygpath --windows $ADDRESSBOOK_INSTALL_PATH | sed 's,\\\\,/,g'`
    ;;
esac
echo ADDRESSBOOK_INSTALL_PATH=$ADDRESSBOOK_INSTALL_PATH
AC_SUBST(ADDRESSBOOK_INSTALL_PATH)

#
# Handle --with-addressbook-build-path
#
AC_ARG_WITH(addressbook-build-path, [
  --with-addressbook-build-path=DIR
                          path to an AddressBook build [[NONE]]],
    [ ADDRESSBOOK_BUILD_PATH=$withval ],
    [ ADDRESSBOOK_BUILD_PATH=NONE ]
    )
if test "x$ADDRESSBOOK_BUILD_PATH" = xNONE; then
    ADDRESSBOOK_BUILD_PATH=''
else
    test -d $ADDRESSBOOK_BUILD_PATH || AC_MSG_ERROR([$ADDRESSBOOK_BUILD_PATH does not exist])
    ADDRESSBOOK_BUILD_PATH=`cd $ADDRESSBOOK_BUILD_PATH && pwd`
    case $IFOLDER_OS in
        windows)
            ADDRESSBOOK_BUILD_PATH=`cygpath --windows $ADDRESSBOOK_BUILD_PATH | sed 's,\\\\,/,g'`
        ;;
    esac
fi
echo ADDRESSBOOK_BUILD_PATH=$ADDRESSBOOK_BUILD_PATH
AC_SUBST(ADDRESSBOOK_BUILD_PATH)

DISTCHECK_CONFIGURE_FLAGS="--with-simias-install-path=$SIMIAS_INSTALL_PATH --with-addressbook-install-path=$ADDRESSBOOK_INSTALL_PATH"
AC_SUBST(DISTCHECK_CONFIGURE_FLAGS)


#
# Handle --with-ndoc-path
#
AC_ARG_WITH(ndoc-path, [
  --with-ndoc-path=PATH        path to dir that contains NDocConsole.exe [[NONE]]],
    [ NDOC_PATH="$withval" ],
    [ NDOC_PATH=NONE ]
    )
if test "$NDOC_PATH" = "NONE"; then
    NDOC_PATH=''
else
    if test `basename "$NDOC_PATH"` = "NDocConsole.exe"; then
        NDOC_PATH=`dirname "$NDOC_PATH"`
    fi
    test -d "$NDOC_PATH" || AC_MSG_ERROR([$NDOC_PATH does not exist])
    NDOC_PATH=`cd "$NDOC_PATH" && pwd`
fi
if test -n "$NDOC_PATH"; then
    if test -e "$NDOC_PATH/NDocConsole.exe"; then
        NDOC_CMD="$NDOC_PATH/NDocConsole.exe"
    fi
    test -e "$NDOC_CMD" || AC_MSG_ERROR([NDocConsole.exe not found in $NDOC_PATH])
fi
echo NDOC_CMD=$NDOC_CMD
AC_SUBST(NDOC_CMD)
AM_CONDITIONAL(NDOC, test -n "$NDOC_CMD")

#
# Handle --enable-debug
#
AC_ARG_ENABLE(debug, [
  --enable-debug          configure the Makefiles to build in DEBUG mode],
    [case "${enableval}" in
        yes) enable_debug=true ;;
        no)  enable_debug=false ;;
        *) AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
    esac],[enable_debug=false])
AM_CONDITIONAL(DEBUG, test x$enable_debug = xtrue)
if test "$enable_debug" = "true"
then
  # Build debug version.
  CFLAGS="$CFLAGS_DEBUG $CFLAGS"
  CSCFLAGS="$CSCFLAGS_DEBUG $CSCFLAGS"
  CXXFLAGS="$CXXFLAGS_DEBUG $CXXFLAGS"
  DEVENV_CONFIGURATION=Debug
  MONO=$MONO_DEBUG
  NUNIT_FLAGS="$DEBUG_NUNITFLAGS $NUNIT_FLAGS"
else
  # Build optimized version.
  CFLAGS="$CFLAGS_OPTIMIZE $CFLAGS"
  CSCFLAGS="$CSCFLAGS_OPTIMIZE $CSCFLAGS"
  CXXFLAGS="$CXXFLAGS_OPTIMIZE $CXXFLAGS"
  DEVENV_CONFIGURATION=Release
  NUNIT_FLAGS="$OPTIMIZE_NUNITFLAGS $NUNIT_FLAGS"
fi
AC_SUBST(DEVENV_CONFIGURATION)

#
# Configure PKG_CONFIG
#
AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
if test "x$PKG_CONFIG" = "xno"; then
    AC_MSG_ERROR([You need to install pkg-config])
fi
case $IFOLDER_OS in
    linux)
        PKG_CHECK_MODULES(BASE_DEPENDENCIES, gtk-sharp >= $GTK_SHARP_MINIMUM_VERSION)
	GTK_SHARP_PREFIX=`pkg-config --variable=prefix gtk-sharp`
	GTK_SHARP_VERSION=`pkg-config --modversion gtk-sharp`
	test -x "$GTK_SHARP_PREFIX/bin/gapi-codegen" || AC_MSG_ERROR([You need to install gtk-sharp-gapi $GTK_SHARP_VERSION])
	AC_SUBST(GTK_SHARP_PREFIX)
	
        PKG_CHECK_MODULES(GDK20, gdk-2.0 >= $GDK20_MINIMUM_VERSION)
	AC_SUBST(GDK20_LIBS)
	AC_SUBST(GDK20_CFLAGS)
        PKG_CHECK_MODULES(ATK, atk >= $ATK_MINIMUM_VERSION)
	AC_SUBST(ATK_LIBS)
	AC_SUBST(ATK_CFLAGS)
    ;;
esac

#
# We handle the --prefix and --with-{simias|addressbook}-{build|install}-path options
# so we remove them and pass all other configuration options on to any subproject
# we configure.
#
if expr "$PASSALONG_CONFIG_OPTIONS" : '.*--prefix=[[^ ]][[^ ]]* *' > /dev/null; then
    PASSALONG_CONFIG_OPTIONS=`echo "$PASSALONG_CONFIG_OPTIONS" | sed 's,--prefix=[[^ ]][[^ ]]* *,,'`
fi
if expr "$PASSALONG_CONFIG_OPTIONS" : '.*--with-simias-build-path=[[^ ]][[^ ]]* *' > /dev/null; then
    PASSALONG_CONFIG_OPTIONS=`echo "$PASSALONG_CONFIG_OPTIONS" | sed 's/--with-simias-build-path=[[^ ]][[^ ]]* *//'`
fi
if expr "$PASSALONG_CONFIG_OPTIONS" : '.*--with-simias-install-path=[[^ ]][[^ ]]* *' > /dev/null; then
    PASSALONG_CONFIG_OPTIONS=`echo "$PASSALONG_CONFIG_OPTIONS" | sed 's/--with-simias-install-path=[[^ ]][[^ ]]* *//'`
fi
if expr "$PASSALONG_CONFIG_OPTIONS" : '.*--with-addressbook-build-path=[[^ ]][[^ ]]* *' > /dev/null; then
    PASSALONG_CONFIG_OPTIONS=`echo "$PASSALONG_CONFIG_OPTIONS" | sed 's/--with-addressbook-build-path=[[^ ]][[^ ]]* *//'`
fi
if expr "$PASSALONG_CONFIG_OPTIONS" : '.*--with-addressbook-install-path=[[^ ]][[^ ]]* *' > /dev/null; then
    PASSALONG_CONFIG_OPTIONS=`echo "$PASSALONG_CONFIG_OPTIONS" | sed 's/--with-addressbook-install-path=[[^ ]][[^ ]]* *//'`
fi
echo PASSALONG_CONFIG_OPTIONS="$PASSALONG_CONFIG_OPTIONS"

#
# Configure Simias subproject, if specified, only if not configuring AddressBook.
# Otherwise, Simias will be configured when AddressBook is configured.
#
if test -n "$SIMIAS_BUILD_PATH" && test -z "$ADDRESSBOOK_BUILD_PATH"; then
    CONFIG_OPTIONS="$PASSALONG_CONFIG_OPTIONS"
    if test -n "$SIMIAS_INSTALL_PATH"; then
        CONFIG_OPTIONS="--prefix=$SIMIAS_INSTALL_PATH ${CONFIG_OPTIONS}"
    else
        test "x$prefix" != xNONE &&
            CONFIG_OPTIONS="--prefix=${prefix} ${CONFIG_OPTIONS}"
    fi
    if test -x "$SIMIAS_BUILD_PATH/configure"; then
        CONFIG_CMD="./configure"
    else
        CONFIG_CMD="./autogen.sh"
    fi
    CONFIG_CMD="$CONFIG_CMD $CONFIG_OPTIONS"
    echo "----------------------------------------------------------------------"
    echo "cd $SIMIAS_BUILD_PATH; $CONFIG_CMD"
    (cd $SIMIAS_BUILD_PATH; eval $CONFIG_CMD)
    echo "----------------------------------------------------------------------"
    SUBPROJECT_BUILD_PATHS="$SIMIAS_BUILD_PATH"
    echo "continuing configure in `pwd`"
fi

#
# Configure AddressBook subproject, if specified.
#
if test -n "$ADDRESSBOOK_BUILD_PATH"; then
    CONFIG_OPTIONS=$PASSALONG_CONFIG_OPTIONS
    test -n "$SIMIAS_INSTALL_PATH" &&
        CONFIG_OPTIONS="--with-simias-install-path=$SIMIAS_INSTALL_PATH ${CONFIG_OPTIONS}"
    test -n "$SIMIAS_BUILD_PATH" &&
        CONFIG_OPTIONS="--with-simias-build-path=$SIMIAS_BUILD_PATH ${CONFIG_OPTIONS}"
    if test -n "$ADDRESSBOOK_INSTALL_PATH"; then
        CONFIG_OPTIONS="--prefix=$ADDRESSBOOK_INSTALL_PATH ${CONFIG_OPTIONS}"
    else
        test "x$prefix" != xNONE &&
            CONFIG_OPTIONS="--prefix=${prefix} ${CONFIG_OPTIONS}"
    fi
    if test -x "$ADDRESSBOOK_BUILD_PATH/configure"; then
        CONFIG_CMD="./configure"
    else
        CONFIG_CMD="./autogen.sh"
    fi
    CONFIG_CMD="$CONFIG_CMD  $CONFIG_OPTIONS"
    echo "----------------------------------------------------------------------"
    echo "cd $ADDRESSBOOK_BUILD_PATH; $CONFIG_CMD"
    (cd $ADDRESSBOOK_BUILD_PATH; eval $CONFIG_CMD)
    echo "----------------------------------------------------------------------"
    SUBPROJECT_BUILD_PATHS="$ADDRESSBOOK_BUILD_PATH"
    echo "continuing configure in `pwd`"
fi
AC_SUBST(SUBPROJECT_BUILD_PATHS)
AM_CONDITIONAL(BUILD_SUBPROJECTS, test -n "$SUBPROJECT_BUILD_PATHS")

#
# Configure files
#
AC_OUTPUT([
Makefile
src/Makefile
src/iFolder/Makefile
src/GtkTrayApp/Makefile
src/GtkTrayApp/ifolder
src/GtkTrayApp/invitationwizard
src/iFolderNautilus/Makefile
src/FormsTrayApp/Makefile
src/FormsInvitationWizard/Makefile
src/iFolderShell/Makefile
src/TrayIcon/Makefile
src/TrayIcon/libtrayicon/Makefile
package/Makefile
package/windows/Makefile
package/linux/Makefile
package/linux/ifolder.spec
package/linux/ifolder.pc
doc/Makefile
])
