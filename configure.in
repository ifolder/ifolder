AC_INIT(src/iFolder/iFolder.cs)
AC_CANONICAL_SYSTEM
AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(ifolder, 0.8)
VERSION=$VERSION.`date +%Y%m%d`
AM_MAINTAINER_MODE
AC_PROG_CC

#
# Check for a valid operating system and C# compiler
#
case $host_os in
    linux*)
        IFOLDER_OS='linux'
        AC_CHECK_PROG(CSC, mcs, mcs)
        test -z "$CSC" && AC_MSG_ERROR([no C Sharp compiler: mcs not found in \$PATH])
    ;;
    darwin*)
        IFOLDER_OS='darwin'
        AC_CHECK_PROG(CSC, mcs, mcs)
        test -z "$CSC" && AC_MSG_ERROR([no C Sharp compiler: mcs not found in \$PATH])
    ;;
    cygwin*)
        IFOLDER_OS='windows'
        AC_CHECK_PROG(CSC, csc, csc)
        test -z "$CSC" && AC_CHECK_PROG(CSC, mcs, mcs)
        test -z "$CSC" && AC_MSG_ERROR([no C Sharp compiler: neither csc nor mcs found in \$PATH])
    ;;
    *)
        AC_MSG_ERROR([Unknown host_os: $host_os])
    ;;
esac
AC_SUBST(IFOLDER_OS)
AM_CONDITIONAL(LINUX, test "$IFOLDER_OS" = "linux")
AM_CONDITIONAL(WINDOWS, test "$IFOLDER_OS" = "windows")
AM_CONDITIONAL(DARWIN, test "$IFOLDER_OS" = "darwin")

#
# We add $prefix to PKG_CONFIG_PATH so pkg-config will find any other
# packages 'make install'-ed to the same prefix.
#
# The default value of $prefix is not set until later in the script.
# We set the default here, if needed.
#
echo prefix=${prefix}
CONFIGURED_PREFIX="${prefix}"
test -z "$CONFIGURED_PREFIX" -o "$CONFIGURED_PREFIX" = "NONE" && CONFIGURED_PREFIX=$ac_default_prefix
echo CONFIGURED_PREFIX=$CONFIGURED_PREFIX
echo PKG_CONFIG_PATH=$PKG_CONFIG_PATH
if test -z "$PKG_CONFIG_PATH"; then
    export PKG_CONFIG_PATH="$CONFIGURED_PREFIX/lib/pkgconfig"
else
    export PKG_CONFIG_PATH="$CONFIGURED_PREFIX/lib/pkgconfig:$PKG_CONFIG_PATH"
fi
echo PKG_CONFIG_PATH=$PKG_CONFIG_PATH
AC_SUBST(PKG_CONFIG_PATH)

#
# Configure C# compiler settings
#
case $CSC in
    #
    # Mono-specific configuration
    #
    mcs)
        CSC_EXEFLAG=/target:exe
        CSC_LIBFLAG=/target:library
        CSC_WINEXEFLAG=/target:winexe
        CSCFLAGS='/d:MONO /warn:4 /d:TRACE'
        CSCFLAGS_DEBUG="/debug+ /d:DEBUG"
        CSCFLAGS_OPTIMIZE="/optimize+"
        MONO=mono
        MONO_DEBUG='mono --debug'
        MONO_PATH="\$(subst \$(SPACE),:,\$(addprefix \$(SRCDIR)\$(SEP),\$(COMMON_SUBDIRS))):`pkg-config --variable=libdir addressbook`:`pkg-config --variable=libdir simias`"
        SYSTEM_XML='System.Xml.dll'
    ;;
    #
    # .NET-specific configuration
    #
    csc)
        CSC_EXEFLAG=/target:exe
        CSC_LIBFLAG=/target:library
        CSC_WINEXEFLAG=/target:winexe
        CSCFLAGS='/d:DOTNET /warn:4 /d:TRACE /nologo'
        CSCFLAGS_DEBUG="/debug+ /d:DEBUG"
        CSCFLAGS_OPTIMIZE="/optimize+"
        MONO=
        MONO_DEBUG=
        MONO_PATH=
        SYSTEM_XML='System.XML.dll'
    ;;
esac
AC_SUBST(CSC)
AC_SUBST(CSC_EXEFLAG)
AC_SUBST(CSC_LIBFLAG)
AC_SUBST(CSC_WINEXEFLAG)
AC_SUBST(CSCFLAGS)
AC_SUBST(MONO)
AC_SUBST(MONO_PATH)
AC_SUBST(SYSTEM_XML)

SRCDIR='$(top_srcdir)/src'
TOOLDIR='$(top_srcdir)/tools'
DOCDIR="$SRCDIR/doc"

AC_SUBST(SRCDIR)
AC_SUBST(TOOLDIR)
AC_SUBST(DOCDIR)

EMPTY=
SPACE='$(EMPTY) $(EMPTY)'

AC_SUBST(EMPTY)
AC_SUBST(SPACE)

COMMON_SUBDIRS="iFolder"
LINUX_SUBDIRS="TrayIcon GtkTrayApp"
WINDOWS_SUBDIRS="FormsTrayApp FormsInvitationWizard iFolderShell"
NOBUILD_SUBDIRS="iFolderNautilus"

AC_SUBST(COMMON_SUBDIRS)
AC_SUBST(LINUX_SUBDIRS)
AC_SUBST(WINDOWS_SUBDIRS)
AC_SUBST(NOBUILD_SUBDIRS)

COMMON_LIBS="/r:iFolder.dll"

COMMON_CLEAN_FILES='*.dbg *.exe *.dll *.pdb */*.pdb *.doc.xml */*.doc.xml *.test.xml */*.test.xml'
COMMON_DISTCLEAN_FILES='*.suo */*.suo *.csproj.user */*.csproj.user bin obj */bin */obj'
COMMON_MAINTAINER_CLEAN_FILES='Makefile.in'
AC_SUBST(COMMON_CLEAN_FILES)
AC_SUBST(COMMON_DISTCLEAN_FILES)
AC_SUBST(COMMON_MAINTAINER_CLEAN_FILES)

#
# Set platform-specific variables
#
case $IFOLDER_OS in
    #
    # Linux-specific configuration
    #
    linux)
        #
        # Set variables
        #
        EMPTY=
        EXE_EXT=''
        ICON_EXT='.ico'
        ICON_FLAG='/resource:'
        NUNIT="$TOOLDIR/NUnit/mono/bin/nunit-console.exe"
        NUNIT_LIBPATH="$TOOLDIR/mono/NUnit/bin"
        NUNIT_FLAGS='/nologo'
        NUNITFLAGS_DEBUG=''
        NUNITFLAGS_OPTIMIZE=''
        OBJ_EXT='.o'
        PLATFORM_SUBDIRS=$LINUX_SUBDIRS
        REPORT_DIR='$(DESTDIR)$(bindir)'
        REPORT_EXE='mono $(top_srcdir)/tools/Report/Report.exe'
        SEP='/'
        SHARED_LIB_EXT='.so'
        SHARED_LIB_FLAG='-shared'
        SQLITELIB_PATH="../../external/sqlite/linux"
        SQLITELIB="libsqlite.so"
        SYSTEM_XML='System.Xml.dll'
        #
        # Set pkg-config variables
        #
        MONO_MINIMUM_VERSION=0.91
        AC_SUBST(MONO_MINIMUM_VERSION)
        
        GTK_SHARP_MINIMUM_VERSION=0.17
        AC_SUBST(GTK_SHARP_MINIMUM_VERSION)
	
        GDK20_MINIMUM_VERSION=2.2.3
        ATK_MINIMUM_VERSION=1.2.4
        
        PKG_CONFIG_REQUIRES="gtk-sharp >= $GTK_SHARP_MINIMUM_VERSION, gdk-2.0 >= $GDK20_MINIMUM_VERSION, atk >= $ATK_MINIMUM_VERSION"
        PKG_CONFIG_CONFLICTS=""
        PKG_CONFIG_LIBS="$COMMON_LIBS /r:TrayIcon.dll"
        
        #
        # Remove this when Makefile.am files are modified to SIMIAS_LIBS and ADDRESSBOOK_LIBS
        #
        SIMIAS_INSTALL_PATH=`pkg-config --variable=prefix simias`
        ADDRESSBOOK_INSTALL_PATH=`pkg-config --variable=prefix addressbook`
    ;;
    #
    # Windows-specific configuration
    #
    windows)
        #
        # Check for valid compilers
        #
        AC_CHECK_PROG(CC, cl, cl)
        test -z "$CC" && AC_MSG_ERROR([no acceptable C compiler found in \$PATH])
        AC_CHECK_PROG(CXX, cl, cl)
        test -z "$CXX" && AC_MSG_ERROR([no acceptable C++ compiler found in \$PATH])
        AC_CHECK_PROG(LD, link, link)
        test -z "$LD" && AC_MSG_ERROR([no acceptable linker found in \$PATH])
        #
        # Set variables
        #
        CC='cl -c'
        CXX='cl -c'
        CDEF='-D'
        CFLAGS="-nologo -W3 -DWIN32 -D_WINDOWS -DWINDOWS -DPACKAGE=\$(PACKAGE) -DVERSION=\$(VERSION)";
        CFLAGS_DEBUG="-MDd -Od -Zi -RTC1 -DDEBUG -D_DEBUG"
        CFLAGS_OPTIMIZE="-MD -O2 -DNDEBUG"
        CINC='-I'
        COUT='-Fo'
        CXXFLAGS="-nologo -GX -W3 -DWIN32 -D_WINDOWS -DWINDOWS -DPACKAGE=\$(PACKAGE) -DVERSION=\$(VERSION)";
        CXXFLAGS_DEBUG="-MDd -Od -Zi -RTC1 -DDEBUG -D_DEBUG"
        CXXFLAGS_OPTIMIZE="-MD -O2 -DNDEBUG"
        DEF='-def:'
        EMPTY=
        EXE_EXT='.exe'
        ICON_EXT='.ico'
        ICON_FLAG='/win32icon:'
        LDFLAGS='-nologo -machine:X86'
        LDFLAGS_DEBUG="-DEBUG"
        LDINC='-libpath:'
        LDOUT='-out:'
        LIB_EXT='.lib'
        LIB_PRE=''
        LIBFLAG=''
        LIBS='kernel32.lib user32.lib gdi32.lib winspool.lib comdlg32.lib advapi32.lib shell32.lib ole32.lib oleaut32.lib uuid.lib odbc32.lib odbccp32.lib rpcrt4.lib ws2_32.lib imagehlp.lib'
        NUNIT="$TOOLDIR/NUnit/net/bin/nunit-console.exe"
        NUNIT_LIBPATH="$TOOLDIR/NUnit/net/bin"
        NUNIT_FLAGS='/nologo'
        NUNITFLAGS_DEBUG=''
        NUNITFLAGS_OPTIMIZE=''
        OBJ_EXT='.obj'
        PLATFORM_SUBDIRS=$WINDOWS_SUBDIRS
        RC='rc'
        RC_FLAGS='-r'
        REPORT_DIR='$(shell cygpath --windows $(DESTDIR)$(bindir) | sed ''s,\\\\,/,g'')'
        REPORT_EXE='$(top_srcdir)/tools/Report/Report.exe'
        RES_EXT='.res'
        SEP='$(EMPTY)\\$(EMPTY)'
        SHARED_LIB_EXT='.dll'
        SHARED_LIB_FLAG='-dll'
        SQLITELIB="sqlite.dll"
        SQLITELIB_PATH="../../external/sqlite/w32"
        SYSTEM_XML='System.XML.dll'
        TLBX='tlbexp'
        TYPE_LIB_EXT='.tlb'
        #
        # Set pkg-config variables
        #
        PKG_CONFIG_REQUIRES=""
        PKG_CONFIG_CONFLICTS=""
        PKG_CONFIG_LIBS="$COMMON_LIBS /r:iFolderComponent.dll"
        
        #
        # Remove this when Makefile.am files are modified to SIMIAS_LIBS and ADDRESSBOOK_LIBS
        #
        SIMIAS_INSTALL_PATH=`pkg-config --variable=prefix simias`
        SIMIAS_INSTALL_PATH=`cygpath --windows $SIMIAS_INSTALL_PATH | sed 's,\\\\,/,g'`
        
        ADDRESSBOOK_INSTALL_PATH=`pkg-config --variable=prefix addressbook`
        ADDRESSBOOK_INSTALL_PATH=`cygpath --windows $ADDRESSBOOK_INSTALL_PATH | sed 's,\\\\,/,g'`
    ;;
esac
AC_SUBST(SIMIAS_INSTALL_PATH)
AC_SUBST(ADDRESSBOOK_INSTALL_PATH)

AC_SUBST(CDEF)
AC_SUBST(CINC)
AC_SUBST(COUT)
AC_SUBST(CXX)
AC_SUBST(CXXFLAGS)
AC_SUBST(DEF)
AC_SUBST(DOCDIR)
AC_SUBST(ICON_EXT)
AC_SUBST(ICON_FLAG)
AC_SUBST(LDFLAGS)
AC_SUBST(LDINC)
AC_SUBST(LDOUT)
AC_SUBST(LIBS)
AC_SUBST(NUNIT)
AC_SUBST(NUNIT_FLAGS)
AC_SUBST(NUNIT_LIBPATH)
AC_SUBST(OBJ_EXT)
AC_SUBST(PKG_CONFIG_REQUIRES)
AC_SUBST(PKG_CONFIG_CONFLICTS)
AC_SUBST(PKG_CONFIG_LIBS)
AC_SUBST(PLATFORM_SUBDIRS)
AC_SUBST(RC)
AC_SUBST(RCFLAGS)
AC_SUBST(REPORT_DIR)
AC_SUBST(REPORT_EXE)
AC_SUBST(RES_EXT)
AC_SUBST(SEP)
AC_SUBST(SHARED_LIB_FLAG)
AC_SUBST(SYSTEM_XML)
AC_SUBST(TLBX)

#
# Run standard macros
#
AM_PROG_CC_STDC
AC_PROG_INSTALL
AC_HEADER_STDC
AM_PROG_LIBTOOL


#
# Handle --with-ndoc-path
#
AC_ARG_WITH(ndoc-path, [
  --with-ndoc-path=PATH        path to dir that contains NDocConsole.exe [[NONE]]],
    [ NDOC_PATH="$withval" ],
    [ NDOC_PATH=NONE ]
    )
if test "$NDOC_PATH" = "NONE"; then
    NDOC_PATH=''
else
    if test `basename "$NDOC_PATH"` = "NDocConsole.exe"; then
        NDOC_PATH=`dirname "$NDOC_PATH"`
    fi
    test -d "$NDOC_PATH" || AC_MSG_ERROR([$NDOC_PATH does not exist])
    NDOC_PATH=`cd "$NDOC_PATH" && pwd`
fi
if test -n "$NDOC_PATH"; then
    if test -e "$NDOC_PATH/NDocConsole.exe"; then
        NDOC_CMD="$NDOC_PATH/NDocConsole.exe"
    fi
    test -e "$NDOC_CMD" || AC_MSG_ERROR([NDocConsole.exe not found in $NDOC_PATH])
fi
echo NDOC_CMD=$NDOC_CMD
AC_SUBST(NDOC_CMD)
AM_CONDITIONAL(NDOC, test -n "$NDOC_CMD")

#
# Handle --enable-debug
#
AC_ARG_ENABLE(debug, [
  --enable-debug          configure the Makefiles to build in DEBUG mode],
    [case "${enableval}" in
        yes) enable_debug=true ;;
        no)  enable_debug=false ;;
        *) AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
    esac],[enable_debug=false])
AM_CONDITIONAL(DEBUG, test x$enable_debug = xtrue)
if test "$enable_debug" = "true"
then
  # Build debug version.
  CFLAGS="$CFLAGS_DEBUG $CFLAGS"
  CSCFLAGS="$CSCFLAGS_DEBUG $CSCFLAGS"
  CXXFLAGS="$CXXFLAGS_DEBUG $CXXFLAGS"
  DEVENV_CONFIGURATION=Debug
  MONO=$MONO_DEBUG
  NUNIT_FLAGS="$NUNITFLAGS_DEBUG $NUNIT_FLAGS"
else
  # Build optimized version.
  CFLAGS="$CFLAGS_OPTIMIZE $CFLAGS"
  CSCFLAGS="$CSCFLAGS_OPTIMIZE $CSCFLAGS"
  CXXFLAGS="$CXXFLAGS_OPTIMIZE $CXXFLAGS"
  DEVENV_CONFIGURATION=Release
  NUNIT_FLAGS="$NUNITFLAGS_OPTIMIZE $NUNIT_FLAGS"
fi
AC_SUBST(DEVENV_CONFIGURATION)

#
# Handle --enable-merge-module
#
AC_ARG_ENABLE(merge-module,
    AC_HELP_STRING([--enable-merge-module],
                   [configure the Makefiles to always build a merge module (MSM)]),
    [case "${enableval}" in
        yes) enable_merge-module=true ;;
        no)  enable_merge-module=false ;;
        *) AC_MSG_ERROR(bad value ${enableval} for --enable-merge-module) ;;
    esac],[enable_merge-module=false])
AM_CONDITIONAL(MERGE_MODULE, test x$enable_merge-module = xtrue)

#
# Configure PKG_CONFIG
#
AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
if test "x$PKG_CONFIG" = "xno"; then
    AC_MSG_ERROR([You need to install pkg-config])
fi

#
# Check for required packages
#
PKG_CHECK_MODULES(SIMIAS, simias >= 0)
AC_SUBST(SIMIAS_LIBS)

PKG_CHECK_MODULES(ADDRESSBOOK, addressbook >= 0)
AC_SUBST(ADDRESSBOOK_LIBS)

case $IFOLDER_OS in
    linux)
        PKG_CHECK_MODULES(BASE_DEPENDENCIES, gtk-sharp >= $GTK_SHARP_MINIMUM_VERSION)
    GTK_SHARP_PREFIX=`pkg-config --variable=prefix gtk-sharp`
    GTK_SHARP_VERSION=`pkg-config --modversion gtk-sharp`
#    test -x "$GTK_SHARP_PREFIX/bin/gapi-codegen" || AC_MSG_ERROR([You need to install gtk-sharp-gapi $GTK_SHARP_VERSION])
    AC_SUBST(GTK_SHARP_PREFIX)
    
        PKG_CHECK_MODULES(GDK20, gdk-2.0 >= $GDK20_MINIMUM_VERSION)
    AC_SUBST(GDK20_CFLAGS)
    AC_SUBST(GDK20_LIBS)
        PKG_CHECK_MODULES(ATK, atk >= $ATK_MINIMUM_VERSION)
    AC_SUBST(ATK_CFLAGS)
    AC_SUBST(ATK_LIBS)
    ;;
esac

#
# Configure files
#
AC_OUTPUT([
Makefile
ifolder.pc
src/Makefile
src/iFolder/Makefile
src/GtkTrayApp/Makefile
src/GtkTrayApp/ifolder
src/GtkTrayApp/invitationwizard
src/GtkTrayApp/collectionbrowser
src/iFolderNautilus/Makefile
src/FormsTrayApp/Makefile
src/FormsInvitationWizard/Makefile
src/iFolderShell/Makefile
src/TrayIcon/Makefile
src/TrayIcon/libtrayicon/Makefile
package/Makefile
package/windows/Makefile
package/linux/Makefile
package/linux/ifolder.spec
doc/Makefile
])
