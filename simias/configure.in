AC_INIT(src/Common/Configuration.cs)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(simias, 1.0)
VERSION=$VERSION.`date +%Y%m%d`
AM_MAINTAINER_MODE
AC_PROG_CC

#
# Check for a valid operating system and C# compiler
#
case $host_os in
    linux*)
        SIMIAS_OS='linux'
        AC_CHECK_PROG(CSC, mcs, mcs)
        test -z "$CSC" && AC_MSG_ERROR([no C Sharp compiler: mcs not found in \$PATH])
	    PKG_CONFIG_DIR='lib'
    ;;
    darwin*)
        SIMIAS_OS='darwin'
        AC_CHECK_PROG(CSC, mcs, mcs)
        test -z "$CSC" && AC_MSG_ERROR([no C Sharp compiler: mcs not found in \$PATH])
	    PKG_CONFIG_DIR='lib'
    ;;
    cygwin*)
        SIMIAS_OS='windows'
        AC_CHECK_PROG(CSC, csc, csc)
        test -z "$CSC" && AC_CHECK_PROG(CSC, mcs, mcs)
        test -z "$CSC" && AC_MSG_ERROR([no C Sharp compiler: neither csc nor mcs found in \$PATH])
    	PKG_CONFIG_DIR='.'
    ;;
    *)
        AC_MSG_ERROR([Unknown host_os: $host_os])
    ;;
esac
AC_SUBST(SIMIAS_OS)
AM_CONDITIONAL(LINUX, test "$SIMIAS_OS" = "linux")
AM_CONDITIONAL(WINDOWS, test "$SIMIAS_OS" = "windows")
AM_CONDITIONAL(DARWIN, test "$SIMIAS_OS" = "darwin")
AM_CONDITIONAL(MONO, test "$CSC" = "mcs")
AM_CONDITIONAL(DOTNET, test "$CSC" = "csc")

#
# We add $prefix to PKG_CONFIG_PATH so pkg-config will find any other
# packages 'make install'-ed to the same prefix.
#
# The default value of $prefix is not set until later in the script.
# We set the default here, if needed.
#
test "x$prefix" = xNONE && prefix=$ac_default_prefix
test "x$exec_prefix" = xNONE && exec_prefix="${prefix}"

#
# Installation directory options.
#
# These are normally left unexpanded so users can "make install exec_prefix=/foo".
# However, we want them expanded.
#
bindir="${exec_prefix}/bin"
sbindir="${exec_prefix}/sbin"
libexecdir="${exec_prefix}/libexec"
datadir="${prefix}/share"
sysconfdir="${prefix}/etc"
sharedstatedir="${prefix}/com"
localstatedir="${prefix}/var"
libdir="${exec_prefix}/lib"
includedir="${prefix}/include"
oldincludedir="/usr/include"
infodir="${prefix}/info"
mandir="${prefix}/man"
webdir="${exec_prefix}/web"
webbindir="${exec_prefix}/web/bin"

if test -z "$PKG_CONFIG_PATH"; then
    export PKG_CONFIG_PATH="$prefix/$PKG_CONFIG_DIR/pkgconfig"
else
    export PKG_CONFIG_PATH="$prefix/$PKG_CONFIG_DIR/pkgconfig:$PKG_CONFIG_PATH"
fi
echo PKG_CONFIG_PATH=$PKG_CONFIG_PATH
AC_SUBST(PKG_CONFIG_PATH)

#
# Configure C# compiler settings
#
case $CSC in
    #
    # Mono-specific configuration
    #
    mcs)
        CSC_EXEFLAG=/target:exe
        CSC_LIBFLAG=/target:library
        CSC_WINEXEFLAG=/target:winexe
        CSCFLAGS='/d:MONO /warn:4 /d:TRACE'
        CSCFLAGS_DEBUG="/debug+ /d:DEBUG"
        CSCFLAGS_OPTIMIZE="/optimize+"
        MONO=mono
        MONO_DEBUG='mono --debug'
        MONO_PATH="\$(LOG4NET_DIR):\$(MDNSRESPONDER_DIR):\$(NOVELL_SECURITY_DIR):\$(top_srcdir)/external/SecureChannelSinks"
        SYSTEM_XML='System.Xml.dll'
    ;;
    #
    # .NET-specific configuration
    #
    csc)
        CSC_EXEFLAG=/target:exe
        CSC_LIBFLAG=/target:library
        CSC_WINEXEFLAG=/target:winexe
        CSCFLAGS='/d:DOTNET /warn:4 /d:TRACE /nologo'
        CSCFLAGS_DEBUG="/debug+ /d:DEBUG"
        CSCFLAGS_OPTIMIZE="/optimize+"
        MONO=
        MONO_DEBUG=
        MONO_PATH=
        SYSTEM_XML='System.XML.dll'
    ;;
esac
AC_SUBST(CSC)
AC_SUBST(CSC_EXEFLAG)
AC_SUBST(CSC_LIBFLAG)
AC_SUBST(CSC_WINEXEFLAG)
AC_SUBST(CSCFLAGS)
AC_SUBST(CFLAGS)
AC_SUBST(MONO)
AC_SUBST(MONO_PATH)
AC_SUBST(SYSTEM_XML)

SRCDIR='$(top_srcdir)/src'
TOOLDIR='$(top_srcdir)/tools'
EXTERNAL_DIR='$(top_srcdir)/external'
DOCDIR="$SRCDIR/doc"

AC_SUBST(SRCDIR)
AC_SUBST(TOOLDIR)
AC_SUBST(DOCDIR)

EMPTY=
SPACE='$(EMPTY) $(EMPTY)'

AC_SUBST(EMPTY)
AC_SUBST(SPACE)

COMMON_CLEAN_FILES='*.dbg *.pdb */*.pdb *.doc.xml */*.doc.xml *.test.xml */*.test.xml'
COMMON_DISTCLEAN_FILES='*.suo */*.suo *.csproj.user */*.csproj.user bin obj */bin */obj'
COMMON_MAINTAINER_CLEAN_FILES='Makefile.in'
AC_SUBST(COMMON_CLEAN_FILES)
AC_SUBST(COMMON_DISTCLEAN_FILES)
AC_SUBST(COMMON_MAINTAINER_CLEAN_FILES)

#
# Set platform-specific variables
#
case $SIMIAS_OS in
    #
    # Darwin-specific configuration
    #
    darwin)
        #
        # Set variables
        #
        CFLAGS_DEBUG="-DDEBUG"
        CSCFLAGS="/d:DARWIN $CSCFLAGS"
        EXE_EXT=''
        ICON_EXT='.ico'
        ICON_FLAG='/resource:'
        INSTALL_EXE_OPTS='--mode=644'
        LOG4NET_DIR="$EXTERNAL_DIR/log4net/bin/mono/1.0/release"
        NOVELL_SECURITY_DIR="$EXTERNAL_DIR/Novell.Security/mono/bin"
        MDNSRESPONDER_DIR="$EXTERNAL_DIR/mDnsResponder/mono/bin"
        NUNIT="mono nunit-console.exe"
        NUNIT_LIBPATH="$TOOLDIR/NUnit/mono/bin"
        NUNIT_FLAGS='/nologo'
        NUNITFLAGS_DEBUG=''
		MONO=mono
        NUNITFLAGS_OPTIMIZE=''
        OBJ_EXT='.o'
        REPORT_DIR='$(DESTDIR)$(webbindir)'
        REPORT_EXE='mint $(top_srcdir)/tools/Report/Report.exe'
        SEP='/'
        SHARED_LIB_EXT=
        SHARED_LIB_FLAG='-dylib'
        STORELIB="sqlite"
        STORELIB_PATH="$EXTERNAL_DIR/sqlite/darwin"
		LDOUT='-o '
		LD='$(CXX) -dynamiclib '
		StoreProviderAssembly="Simias.dll"
		StoreProviderType="Simias.Storage.Provider.Flaim.FlaimProvider"
		GSOAP_PATH="$TOOLDIR/gsoap/macosx-2.7"
		#StoreProviderAssembly="SqliteProvider.dll"
		#StoreProviderType="Simias.Storage.Provider.Sqlite.SqliteProvider"
    ;;
    #
    # Linux-specific configuration
    #
    linux)
        #
        # Set variables
        #
        CFLAGS_DEBUG="-DDEBUG"
        CSCFLAGS="/d:LINUX $CSCFLAGS"
        EXE_EXT=''
        ICON_EXT='.ico'
        ICON_FLAG='/resource:'
        INSTALL_EXE_OPTS='--mode=644'
        LOG4NET_DIR="$EXTERNAL_DIR/log4net/bin/mono/1.0/release"
        NOVELL_SECURITY_DIR="$EXTERNAL_DIR/Novell.Security/mono/bin"
        MDNSRESPONDER_DIR="$EXTERNAL_DIR/mDnsResponder/mono/bin"
        NUNIT="mono nunit-console.exe"
        NUNIT_LIBPATH="$TOOLDIR/NUnit/mono/bin"
        NUNIT_FLAGS='/nologo'
        NUNITFLAGS_DEBUG=''
        NUNITFLAGS_OPTIMIZE=''
        OBJ_EXT='.o'
        REPORT_DIR='$(DESTDIR)$(webbindir)'
        REPORT_EXE='mono $(top_srcdir)/tools/Report/Report.exe'
        SEP='/'
        SHARED_LIB_EXT='.so'
        SHARED_LIB_FLAG='-shared'
        STORELIB="libsqlite.so"
        STORELIB_PATH="$EXTERNAL_DIR/sqlite/linux"
		LDOUT='-o'
		# have to add this check on redhat for some reason
        AC_CHECK_PROG(CXX, g++, g++)
		StoreProviderAssembly="Simias.dll"
		StoreProviderType="Simias.Storage.Provider.Flaim.FlaimProvider"
		GSOAP_PATH="$TOOLDIR/gsoap/linux-2.7"
    ;;
    #
    # Windows-specific configuration
    #
    windows)
        #
        # Check for valid compilers
        #
        AC_CHECK_PROG(CC, cl, cl)
        test -z "$CC" && AC_MSG_ERROR([no acceptable C compiler found in \$PATH])
        AC_CHECK_PROG(CXX, cl, cl)
        test -z "$CXX" && AC_MSG_ERROR([no acceptable C++ compiler found in \$PATH])
        AC_CHECK_PROG(LD, link, link)
        test -z "$LD" && AC_MSG_ERROR([no acceptable linker found in \$PATH])
        #
        # Set variables
        #
        CC='cl -c'
        CXX='cl -c'
        CDEF='-D'
        CFLAGS="-nologo -W3 -DWIN32 -D_WINDOWS -DWINDOWS -DPACKAGE=\$(PACKAGE) -DVERSION=\$(VERSION)";
        CFLAGS_DEBUG="-MDd -Od -Zi -RTC1 -DDEBUG -D_DEBUG"
        CFLAGS_OPTIMIZE="-MD -O2 -DNDEBUG"
        CINC='-I'
        COUT='-Fo'
        CSCFLAGS="/d:WINDOWS $CSCFLAGS"
        CXXFLAGS="-nologo -GX -W3 -DWIN32 -D_WINDOWS -DWINDOWS -DPACKAGE=\$(PACKAGE) -DVERSION=\$(VERSION)";
        CXXFLAGS_DEBUG="-MDd -Od -Zi -RTC1 -DDEBUG -D_DEBUG"
        CXXFLAGS_OPTIMIZE="-MD -O2 -DNDEBUG"
        DEF='-def:'
        EMPTY=
        EXE_EXT='.exe'
        ICON_EXT='.ico'
        ICON_FLAG='/win32icon:'
        INSTALL_EXE_OPTS=''
        LDFLAGS='-nologo -machine:X86'
        LDFLAGS_DEBUG="-DEBUG"
        LDINC='-libpath:'
        LDOUT='-out:'
        LIB_EXT='.lib'
        LIB_PRE=''
        LIBFLAG=''
        LIBS='kernel32.lib user32.lib gdi32.lib winspool.lib comdlg32.lib advapi32.lib shell32.lib ole32.lib oleaut32.lib uuid.lib odbc32.lib odbccp32.lib rpcrt4.lib ws2_32.lib imagehlp.lib'
        LOG4NET_DIR="$EXTERNAL_DIR/log4net/bin/net/1.0/release"
        NOVELL_SECURITY_DIR="$EXTERNAL_DIR/Novell.Security/net/bin"
        MDNSRESPONDER_DIR="$EXTERNAL_DIR/mDnsResponder/net/bin"
        NUNIT="nunit-console.exe"
        NUNIT_LIBPATH="$TOOLDIR/NUnit/net/bin"
        NUNIT_FLAGS='/nologo'
        NUNITFLAGS_DEBUG=''
        NUNITFLAGS_OPTIMIZE=''
        OBJ_EXT='.obj'
        RC='rc'
        RC_FLAGS='-r'
        REPORT_DIR='$(shell cygpath --windows $(DESTDIR)$(webbindir) | sed ''s,\\\\,/,g'')'
        REPORT_EXE='$(top_srcdir)/tools/Report/Report.exe'
        RES_EXT='.res'
        SEP='$(EMPTY)\\$(EMPTY)'
        SHARED_LIB_EXT='.dll'
        SHARED_LIB_FLAG='-dll'
        STORELIB="sqlite.dll"
        STORELIB_PATH="$EXTERNAL_DIR/sqlite/w32"
        SYSTEM_XML='System.XML.dll'
		GSOAP_PATH="$TOOLDIR/gsoap/WIN32-2.7"
        #
    	# Override Linux-like directory structure
        #
    	bindir="${exec_prefix}"
    	sbindir="${exec_prefix}"
    	libexecdir="${exec_prefix}"
    	libdir="${exec_prefix}"
		StoreProviderAssembly="Simias.dll"
		StoreProviderType="Simias.Storage.Provider.Flaim.FlaimProvider"
    ;;
esac
AC_SUBST(CDEF)
AC_SUBST(CINC)
AC_SUBST(COUT)
AC_SUBST(CXX)
AC_SUBST(CXXFLAGS)
AC_SUBST(DEF)
AC_SUBST(ICON_EXT)
AC_SUBST(ICON_FLAG)
AC_SUBST(INSTALL_EXE_OPTS)
AC_SUBST(LDFLAGS)
AC_SUBST(LDINC)
AC_SUBST(LDOUT)
AC_SUBST(LIB_PRE)
AC_SUBST(LIBS)
AC_SUBST(LOG4NET_DIR)
AC_SUBST(NOVELL_SECURITY_DIR)
AC_SUBST(MDNSRESPONDER_DIR)
AC_SUBST(NUNIT)
AC_SUBST(NUNIT_FLAGS)
AC_SUBST(NUNIT_LIBPATH)
AC_SUBST(OBJ_EXT)
AC_SUBST(REPORT_DIR)
AC_SUBST(REPORT_EXE)
AC_SUBST(SEP)
AC_SUBST(SHARED_LIB_EXT)
AC_SUBST(SHARED_LIB_FLAG)
AC_SUBST(STORELIB)
AC_SUBST(STORELIB_PATH)
AC_SUBST(webdir)
AC_SUBST(webbindir)
AC_SUBST(StoreProviderAssembly)
AC_SUBST(StoreProviderType)
AC_SUBST(GSOAP_PATH)

#
# Set pkg-config variables
#
PKG_CONFIG_REQUIRES=""
PKG_CONFIG_CONFLICTS=""
PKG_CONFIG_LIBS="/r:Simias.dll /r:Novell.AddressBook.dll"
AC_SUBST(PKG_CONFIG_REQUIRES)
AC_SUBST(PKG_CONFIG_CONFLICTS)
AC_SUBST(PKG_CONFIG_LIBS)

CLIENT_PKG_CONFIG_REQUIRES=""
CLIENT_PKG_CONFIG_CONFLICTS=""
CLIENT_PKG_CONFIG_LIBS="/r:SimiasClient.dll"
AC_SUBST(CLIENT_PKG_CONFIG_REQUIRES)
AC_SUBST(CLIENT_PKG_CONFIG_CONFLICTS)
AC_SUBST(CLIENT_PKG_CONFIG_LIBS)

#
# Run standard macros
#
AM_PROG_CC_STDC
AC_PROG_INSTALL
AC_HEADER_STDC
AM_PROG_LIBTOOL

#
# Handle --with-ndoc-path
#
AC_ARG_WITH(ndoc-path, [
  --with-ndoc-path=PATH        path to dir that contains NDocConsole.exe [[NONE]]],
    [ NDOC_PATH="$withval" ],
    [ NDOC_PATH=NONE ]
    )
if test "$NDOC_PATH" = "NONE"; then
    NDOC_PATH=''
else
    if test `basename "$NDOC_PATH"` = "NDocConsole.exe"; then
        NDOC_PATH=`dirname "$NDOC_PATH"`
    fi
    test -d "$NDOC_PATH" || AC_MSG_ERROR([$NDOC_PATH does not exist])
    NDOC_PATH=`cd "$NDOC_PATH" && pwd`
fi
if test -n "$NDOC_PATH"; then
    if test -e "$NDOC_PATH/NDocConsole.exe"; then
        NDOC_CMD="$NDOC_PATH/NDocConsole.exe"
    fi
    test -e "$NDOC_CMD" || AC_MSG_ERROR([NDocConsole.exe not found in $NDOC_PATH])
fi
echo NDOC_CMD=$NDOC_CMD
AC_SUBST(NDOC_CMD)
AM_CONDITIONAL(NDOC, test -n "$NDOC_CMD")
AM_CONDITIONAL(MAKE_DOC_FILE, test -n "$NDOC_CMD")

#
# Handle --enable-debug
#
AC_ARG_ENABLE(debug, [
  --enable-debug          configure the Makefiles to build in DEBUG mode],
    [case "${enableval}" in
        yes) enable_debug=true ;;
        no)  enable_debug=false ;;
        *) AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
    esac],[enable_debug=false])
AM_CONDITIONAL(DEBUG, test x$enable_debug = xtrue)
if test "$enable_debug" = "true"
then
  # Build debug version.
  CFLAGS="$CFLAGS_DEBUG $CFLAGS"
  CSCFLAGS="$CSCFLAGS_DEBUG $CSCFLAGS"
  CXXFLAGS="$CXXFLAGS_DEBUG $CXXFLAGS"
  DEVENV_CONFIGURATION=Debug
  MONO=$MONO_DEBUG
  NUNIT_FLAGS="$NUNITFLAGS_DEBUG $NUNIT_FLAGS"
else
  # Build optimized version.
  CFLAGS="$CFLAGS_OPTIMIZE $CFLAGS"
  CSCFLAGS="$CSCFLAGS_OPTIMIZE $CSCFLAGS"
  CXXFLAGS="$CXXFLAGS_OPTIMIZE $CXXFLAGS"
  DEVENV_CONFIGURATION=Release
  NUNIT_FLAGS="$NUNITFLAGS_OPTIMIZE $NUNIT_FLAGS"
fi
AC_SUBST(DEVENV_CONFIGURATION)

#
# Handle --enable-gaim-domain
#
AC_ARG_ENABLE(gaim-domain, [
  --enable-gaim-domain          Enable and build a Gaim Buddy List Workgroup Domain],
    [case "${enableval}" in
        yes) enable_gaim_domain=true ;;
        no)  enable_gaim_domain=false ;;
        *) AC_MSG_ERROR(bad value ${enableval} for --enable-gaim-domain) ;;
    esac],[enable_gaim_domain=false])
AM_CONDITIONAL(BUILD_GAIM_DOMAIN, test x$enable_gaim_domain = xtrue)
if test "$enable_gaim_domain" = "true"
then
  # Add anything needed specifically for when buiding the gaim-domain
  PC_FILE_LIB_SIMIAS_GAIM="-lsimiasgaim"
  SIMIAS_CONF_SIMIAS_GAIM="<Service name=\"Gaim Domain Service\" assembly=\"Simias.Gaim.dll\" enabled=\"True\" type=\"Thread\" class=\"Simias.Gaim.GaimService\" />"
else
  # Put anything here special for when not building gaim-domain
  PC_FILE_LIB_SIMIAS_GAIM=
  SIMIAS_CONF_SIMIAS_GAIM=
fi
AC_SUBST(PC_FILE_LIB_SIMIAS_GAIM)
AC_SUBST(SIMIAS_CONF_SIMIAS_GAIM)



#
# Configure PKG_CONFIG
#
AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
if test "x$PKG_CONFIG" = "xno"; then
    AC_MSG_ERROR([You need to install pkg-config])
fi

#
# Configure XML2_CONFIG
#
AC_PATH_PROG(XML2_CONFIG, xml2-config, no)
if test "x$XML2_CONFIG" = "xno"; then
    AC_MSG_ERROR([You need to install libxml2 and libxml2-devel])
fi

XML2_CFLAGS='`xml2-config --cflags`'
XML2_LIBS='`xml2-config --libs`'
AC_SUBST(XML2_CFLAGS)
AC_SUBST(XML2_LIBS)

#
# Configure files
#
AC_OUTPUT([
Makefile
simias.pc
simias-client.pc
simias-client-c.pc
src/Makefile
src/SimiasClient/SimiasSetup.cs
src/Common/MyDns.cs
src/Common/simias-client-bootstrap.config
src/SimiasClient/Makefile
src/SimiasClient/libsimias-event/Makefile
src/Simias.dll/Makefile
src/FlaimProvider/Makefile
src/FlaimProvider/FlaimWrapper/Makefile
src/SimiasApp/Makefile
src/SimiasServer/Makefile
src/SimiasServer/simias
src/AddressBook/Makefile
src/mDnsService/Makefile
src/WebService/Makefile
src/POBoxWS/Makefile
src/SyncService/Makefile
src/SqliteProvider/Makefile
src/libsimias/Makefile
src/mDnsDomain/Makefile
test/Makefile
test/ChangeLog/Makefile
test/Common/Makefile
test/Configuration/Makefile
test/CollectionStore/Makefile
test/Smtp/Makefile
test/Location/Makefile
test/SyncCommon/Makefile
test/SyncManager/Makefile
test/SyncEndToEnd/Makefile
test/POBox/Makefile
test/SimiasTestsWS/Makefile
test/SimiasTestsCmd/Makefile
other/Makefile
other/SimpleServer/Makefile
package/Makefile
package/windows/Makefile
package/linux/Makefile
package/darwin/Makefile
package/linux/simias.spec
doc/Makefile
])


