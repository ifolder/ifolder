AC_INIT(src/Common/Library/Configuration.cs)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(simias, 0.10)
VERSION=$VERSION.`date +%Y%m%d`
AM_MAINTAINER_MODE

#
# Check for a valid operating system and C# compiler
#
case $host_os in
    linux*)
        SIMIAS_OS='linux'
        AC_CHECK_PROG(CSC, mcs, mcs)
        test -z "$CSC" && AC_MSG_ERROR([no C Sharp compiler: mcs not found in \$PATH])
	    PKG_CONFIG_DIR='lib'
    ;;
    darwin*)
        SIMIAS_OS='darwin'
        AC_CHECK_PROG(CSC, mcs, mcs)
        test -z "$CSC" && AC_MSG_ERROR([no C Sharp compiler: mcs not found in \$PATH])
	    PKG_CONFIG_DIR='lib'
    ;;
    cygwin*)
        SIMIAS_OS='windows'
        AC_CHECK_PROG(CSC, csc, csc)
        test -z "$CSC" && AC_CHECK_PROG(CSC, mcs, mcs)
        test -z "$CSC" && AC_MSG_ERROR([no C Sharp compiler: neither csc nor mcs found in \$PATH])
    	PKG_CONFIG_DIR='.'
    ;;
    *)
        AC_MSG_ERROR([Unknown host_os: $host_os])
    ;;
esac
AC_SUBST(SIMIAS_OS)
AM_CONDITIONAL(LINUX, test "$SIMIAS_OS" = "linux")
AM_CONDITIONAL(WINDOWS, test "$SIMIAS_OS" = "windows")
AM_CONDITIONAL(DARWIN, test "$SIMIAS_OS" = "darwin")
AM_CONDITIONAL(MONO, test "$CSC" = "mcs")
AM_CONDITIONAL(DOTNET, test "$CSC" = "csc")

#
# We add $prefix to PKG_CONFIG_PATH so pkg-config will find any other
# packages 'make install'-ed to the same prefix.
#
# The default value of $prefix is not set until later in the script.
# We set the default here, if needed.
#
test "x$prefix" = xNONE && prefix=$ac_default_prefix
test "x$exec_prefix" = xNONE && exec_prefix="${prefix}"

#
# Installation directory options.
#
# These are normally left unexpanded so users can "make install exec_prefix=/foo".
# However, we want them expanded.
#
bindir="${exec_prefix}/bin"
sbindir="${exec_prefix}/sbin"
libexecdir="${exec_prefix}/libexec"
datadir="${prefix}/share"
sysconfdir="${prefix}/etc"
sharedstatedir="${prefix}/com"
localstatedir="${prefix}/var"
libdir="${exec_prefix}/lib"
includedir="${prefix}/include"
oldincludedir="/usr/include"
infodir="${prefix}/info"
mandir="${prefix}/man"

if test -z "$PKG_CONFIG_PATH"; then
    export PKG_CONFIG_PATH="$prefix/$PKG_CONFIG_DIR/pkgconfig"
else
    export PKG_CONFIG_PATH="$prefix/$PKG_CONFIG_DIR/pkgconfig:$PKG_CONFIG_PATH"
fi
echo PKG_CONFIG_PATH=$PKG_CONFIG_PATH
AC_SUBST(PKG_CONFIG_PATH)

#
# Configure C# compiler settings
#
case $CSC in
    #
    # Mono-specific configuration
    #
    mcs)
        CSC_EXEFLAG=/target:exe
        CSC_LIBFLAG=/target:library
        CSC_WINEXEFLAG=/target:winexe
        CSCFLAGS='/d:MONO /warn:4 /d:TRACE'
        CSCFLAGS_DEBUG="/debug+ /d:DEBUG"
        CSCFLAGS_OPTIMIZE="/optimize+"
        MONO=mono
        MONO_DEBUG='mono --debug'
        MONO_PATH="\$(LOG4NET_DIR):\$(MDNSRESPONDER_DIR):\$(top_srcdir)/external/SecureChannelSinks"
        SYSTEM_XML='System.Xml.dll'
    ;;
    #
    # .NET-specific configuration
    #
    csc)
        CSC_EXEFLAG=/target:exe
        CSC_LIBFLAG=/target:library
        CSC_WINEXEFLAG=/target:winexe
        CSCFLAGS='/d:DOTNET /warn:4 /d:TRACE /nologo'
        CSCFLAGS_DEBUG="/debug+ /d:DEBUG"
        CSCFLAGS_OPTIMIZE="/optimize+"
        MONO=
        MONO_DEBUG=
        MONO_PATH=
        SYSTEM_XML='System.XML.dll'
    ;;
esac
AC_SUBST(CSC)
AC_SUBST(CSC_EXEFLAG)
AC_SUBST(CSC_LIBFLAG)
AC_SUBST(CSC_WINEXEFLAG)
AC_SUBST(CSCFLAGS)
AC_SUBST(MONO)
AC_SUBST(MONO_PATH)
AC_SUBST(SYSTEM_XML)

SRCDIR='$(top_srcdir)/src'
TOOLDIR='$(top_srcdir)/tools'
EXTERNAL_DIR='$(top_srcdir)/external'
DOCDIR="$SRCDIR/doc"

AC_SUBST(SRCDIR)
AC_SUBST(TOOLDIR)
AC_SUBST(DOCDIR)

EMPTY=
SPACE='$(EMPTY) $(EMPTY)'

AC_SUBST(EMPTY)
AC_SUBST(SPACE)

COMMON_CLEAN_FILES='*.dbg *.pdb */*.pdb *.doc.xml */*.doc.xml *.test.xml */*.test.xml'
COMMON_DISTCLEAN_FILES='*.suo */*.suo *.csproj.user */*.csproj.user bin obj */bin */obj'
COMMON_MAINTAINER_CLEAN_FILES='Makefile.in'
AC_SUBST(COMMON_CLEAN_FILES)
AC_SUBST(COMMON_DISTCLEAN_FILES)
AC_SUBST(COMMON_MAINTAINER_CLEAN_FILES)

#
# Set platform-specific variables
#
case $SIMIAS_OS in
    #
    # Darwin-specific configuration
    #
    darwin)
        #
        # Set variables
        #
        ICON_EXT='.ico'
        ICON_FLAG='/resource:'
        INSTALL_EXE_OPTS='--mode=644'
        LOG4NET_DIR="$EXTERNAL_DIR/log4net/bin/mono/1.0/release"
        MDNSRESPONDER_DIR="$EXTERNAL_DIR/mDnsResponder/mono/bin"
        NUNIT="$TOOLDIR/NUnit/mono/bin/nunit-console.exe"
        NUNIT_LIBPATH="$TOOLDIR/NUnit/mono/bin"
        NUNIT_FLAGS='/nologo'
        NUNITFLAGS_DEBUG=''
	MONO=mono
        NUNITFLAGS_OPTIMIZE=''
        REPORT_DIR='$(DESTDIR)$(bindir)'
        REPORT_EXE='mint $(top_srcdir)/tools/Report/Report.exe'
        SEP='/'
        SQLITELIB="sqlite"
        SQLITELIB_PATH="$EXTERNAL_DIR/sqlite/darwin"
    ;;
    #
    # Linux-specific configuration
    #
    linux)
        #
        # Set variables
        #
        ICON_EXT='.ico'
        ICON_FLAG='/resource:'
        INSTALL_EXE_OPTS='--mode=644'
        LOG4NET_DIR="$EXTERNAL_DIR/log4net/bin/mono/1.0/release"
        MDNSRESPONDER_DIR="$EXTERNAL_DIR/mDnsResponder/mono/bin"
        NUNIT="$TOOLDIR/NUnit/mono/bin/nunit-console.exe"
        NUNIT_LIBPATH="$TOOLDIR/NUnit/mono/bin"
        NUNIT_FLAGS='/nologo'
        NUNITFLAGS_DEBUG=''
        NUNITFLAGS_OPTIMIZE=''
        REPORT_DIR='$(DESTDIR)$(bindir)'
        REPORT_EXE='mono $(top_srcdir)/tools/Report/Report.exe'
        SEP='/'
        SQLITELIB="libsqlite.so"
        SQLITELIB_PATH="$EXTERNAL_DIR/sqlite/linux"
    ;;
    #
    # Windows-specific configuration
    #
    windows)
        ICON_EXT='.ico'
        ICON_FLAG='/win32icon:'
        INSTALL_EXE_OPTS=''
        LOG4NET_DIR="$EXTERNAL_DIR/log4net/bin/net/1.0/release"
        MDNSRESPONDER_DIR="$EXTERNAL_DIR/mDnsResponder/net/bin"
        NUNIT="$TOOLDIR/NUnit/net/bin/nunit-console.exe"
        NUNIT_LIBPATH="$TOOLDIR/NUnit/net/bin"
        NUNIT_FLAGS='/nologo'
        NUNITFLAGS_DEBUG=''
        NUNITFLAGS_OPTIMIZE=''
        REPORT_DIR='$(shell cygpath --windows $(DESTDIR)$(bindir) | sed ''s,\\\\,/,g'')'
        REPORT_EXE='$(top_srcdir)/tools/Report/Report.exe'
        SEP='$(EMPTY)\\$(EMPTY)'
        SQLITELIB="sqlite.dll"
        SQLITELIB_PATH="$EXTERNAL_DIR/sqlite/w32"
        #
    	# Override Linux-like directory structure
        #
    	bindir="${exec_prefix}"
    	sbindir="${exec_prefix}"
    	libexecdir="${exec_prefix}"
    	libdir="${exec_prefix}"
    ;;
esac
AC_SUBST(ICON_EXT)
AC_SUBST(ICON_FLAG)
AC_SUBST(INSTALL_EXE_OPTS)
AC_SUBST(LOG4NET_DIR)
AC_SUBST(MDNSRESPONDER_DIR)
AC_SUBST(NUNIT)
AC_SUBST(NUNIT_FLAGS)
AC_SUBST(NUNIT_LIBPATH)
AC_SUBST(REPORT_DIR)
AC_SUBST(REPORT_EXE)
AC_SUBST(SEP)
AC_SUBST(SQLITELIB)
AC_SUBST(SQLITELIB_PATH)

#
# Set pkg-config variables
#
PKG_CONFIG_REQUIRES=""
PKG_CONFIG_CONFLICTS=""
PKG_CONFIG_LIBS="/r:Simias.dll"
AC_SUBST(PKG_CONFIG_REQUIRES)
AC_SUBST(PKG_CONFIG_CONFLICTS)
AC_SUBST(PKG_CONFIG_LIBS)

#
# Run standard macros
#
#AM_PROG_CC_STDC
#AC_PROG_INSTALL
#AC_HEADER_STDC
#AM_PROG_LIBTOOL

#
# Handle --with-ndoc-path
#
AC_ARG_WITH(ndoc-path, [
  --with-ndoc-path=PATH        path to dir that contains NDocConsole.exe [[NONE]]],
    [ NDOC_PATH="$withval" ],
    [ NDOC_PATH=NONE ]
    )
if test "$NDOC_PATH" = "NONE"; then
    NDOC_PATH=''
else
    if test `basename "$NDOC_PATH"` = "NDocConsole.exe"; then
        NDOC_PATH=`dirname "$NDOC_PATH"`
    fi
    test -d "$NDOC_PATH" || AC_MSG_ERROR([$NDOC_PATH does not exist])
    NDOC_PATH=`cd "$NDOC_PATH" && pwd`
fi
if test -n "$NDOC_PATH"; then
    if test -e "$NDOC_PATH/NDocConsole.exe"; then
        NDOC_CMD="$NDOC_PATH/NDocConsole.exe"
    fi
    test -e "$NDOC_CMD" || AC_MSG_ERROR([NDocConsole.exe not found in $NDOC_PATH])
fi
echo NDOC_CMD=$NDOC_CMD
AC_SUBST(NDOC_CMD)
AM_CONDITIONAL(NDOC, test -n "$NDOC_CMD")

#
# Handle --enable-debug
#
AC_ARG_ENABLE(debug, [
  --enable-debug          configure the Makefiles to build in DEBUG mode],
    [case "${enableval}" in
        yes) enable_debug=true ;;
        no)  enable_debug=false ;;
        *) AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
    esac],[enable_debug=false])
AM_CONDITIONAL(DEBUG, test x$enable_debug = xtrue)
if test "$enable_debug" = "true"
then
  # Build debug version.
  CFLAGS="$CFLAGS_DEBUG $CFLAGS"
  CSCFLAGS="$CSCFLAGS_DEBUG $CSCFLAGS"
  CXXFLAGS="$CXXFLAGS_DEBUG $CXXFLAGS"
  DEVENV_CONFIGURATION=Debug
  MONO=$MONO_DEBUG
  NUNIT_FLAGS="$NUNITFLAGS_DEBUG $NUNIT_FLAGS"
else
  # Build optimized version.
  CFLAGS="$CFLAGS_OPTIMIZE $CFLAGS"
  CSCFLAGS="$CSCFLAGS_OPTIMIZE $CSCFLAGS"
  CXXFLAGS="$CXXFLAGS_OPTIMIZE $CXXFLAGS"
  DEVENV_CONFIGURATION=Release
  NUNIT_FLAGS="$NUNITFLAGS_OPTIMIZE $NUNIT_FLAGS"
fi
AC_SUBST(DEVENV_CONFIGURATION)

#
# Handle --enable-merge-module
#
# On Windows, if set, this will cause 'make all' and 'make install' to include
# building and installing the merge module (MSM) that is to be included in the
# SDK install package.
#
AC_ARG_ENABLE(merge_module,
    AC_HELP_STRING([--enable-merge-module],
                   [configure the Makefiles to always build a merge module (MSM)]),
    [case "${enableval}" in
        yes) enable_merge_module=true ;;
        no)  enable_merge_module=false ;;
        *) AC_MSG_ERROR(bad value ${enableval} for --enable-merge-module) ;;
    esac],[enable_merge_module=false])
AM_CONDITIONAL(MERGE_MODULE, test x$enable_merge_module = xtrue)

#
# Configure PKG_CONFIG
#
AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
if test "x$PKG_CONFIG" = "xno"; then
    AC_MSG_ERROR([You need to install pkg-config])
fi

#
# Configure files
#
AC_OUTPUT([
Makefile
simias.pc
src/Makefile
src/install-simias-to-gac
src/Common/Library/SimiasSetup.cs
src/Simias.dll/Makefile
src/Events/Makefile
src/mDnsService/Makefile
src/SyncManager/Makefile
package/Makefile
package/windows/Makefile
package/linux/Makefile
package/darwin/Makefile
package/linux/simias.spec
doc/Makefile
])

chmod +x src/install-simias-to-gac

