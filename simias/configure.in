AC_INIT(src/Common/Library/Configuration.cs)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(simias, 0.7)
VERSION=$VERSION.`date +%Y%m%d`
AM_MAINTAINER_MODE

#
# Check for a valid C# compiler
#
AC_CHECK_PROG(CSC, csc, csc)
test -z "$CSC" && AC_CHECK_PROG(CSC, mcs, mcs)
test -z "$CSC" && AC_MSG_ERROR([no acceptable C Sharp compiler found in \$PATH])

case $CSC in
    #
    # Mono-specific configuration
    #
    mcs)
        CSC_EXEFLAG=/target:exe
        CSC_LIBFLAG=/target:library
        CSC_WINEXEFLAG=/target:winexe
        CSCFLAGS='/d:MONO /warn:4 /d:TRACE'
        CSCFLAGS_DEBUG="/debug+ /d:DEBUG"
        CSCFLAGS_OPTIMIZE="/optimize+"
        MONO=mono
        MONO_DEBUG='mono --debug'
        MONO_PATH='$(LOG4NET_DIR):$(top_srcdir)/external/SecureChannelSinks:$(subst $(SPACE),:,$(addprefix $(SRCDIR)$(SEP),$(COMMON_SUBDIRS)))'
        SYSTEM_XML='System.Xml.dll'
    ;;
    #
    # .NET-specific configuration
    #
    csc)
        CSC_EXEFLAG=/target:exe
        CSC_LIBFLAG=/target:library
        CSC_WINEXEFLAG=/target:winexe
        CSCFLAGS='/d:DOTNET /warn:4 /d:TRACE /nologo'
        CSCFLAGS_DEBUG="/debug+ /d:DEBUG"
        CSCFLAGS_OPTIMIZE="/optimize+"
        MONO=
        MONO_DEBUG=
        MONO_PATH=
        SYSTEM_XML='System.XML.dll'
    ;;
esac
AC_SUBST(CSC)
AC_SUBST(CSC_EXEFLAG)
AC_SUBST(CSC_LIBFLAG)
AC_SUBST(CSC_WINEXEFLAG)
AC_SUBST(CSCFLAGS)
AC_SUBST(MONO)
AC_SUBST(MONO_PATH)
AC_SUBST(SYSTEM_XML)

SRCDIR='$(top_srcdir)/src'
TOOLDIR='$(top_srcdir)/tools'
EXTERNAL_DIR='$(top_srcdir)/external'
DOCDIR="$SRCDIR/doc"

AC_SUBST(SRCDIR)
AC_SUBST(TOOLDIR)
AC_SUBST(DOCDIR)

EMPTY=
SPACE='$(EMPTY) $(EMPTY)'

AC_SUBST(EMPTY)
AC_SUBST(SPACE)

COMMON_SUBDIRS="Common CollectionEvents UserService Storage StoreProvider FsProvider SqliteProvider CollectionStore FsWatcher RemotingSniffer SyncCommon StoreWatcher Location Agent Sync SyncManager SyncTools AddressBook AddressBookCmd Smtp Invitation MiniShare AcceptanceTests"
LINUX_SUBDIRS="CommonGtkWindows"
WINDOWS_SUBDIRS="CommonFormWindows"
DARWIN_SUBDIRS=

AC_SUBST(COMMON_SUBDIRS)
AC_SUBST(LINUX_SUBDIRS)
AC_SUBST(WINDOWS_SUBDIRS)

#
# Check for a valid operating system
#
case $host_os in
    linux*)
        SIMIAS_OS='linux'
    ;;
    darwin*)
        SIMIAS_OS='darwin'
    ;;
    cygwin*)
        SIMIAS_OS='windows'
    ;;
    *)
        AC_MSG_ERROR([Unknown host_os: $host_os])
    ;;
esac
AC_SUBST(SIMIAS_OS)
AM_CONDITIONAL(LINUX, test "$SIMIAS_OS" = "linux")
AM_CONDITIONAL(WINDOWS, test "$SIMIAS_OS" = "windows")
AM_CONDITIONAL(DARWIN, test "$SIMIAS_OS" = "darwin")

#
# Set platform-specific variables
#
case $SIMIAS_OS in
    #
    # Darwin-specific configuration
    #
    darwin)
        #
        # Set variables
        #
        COMMON_CLEAN_FILES='.cstore .simias'
        ICON_EXT='.ico'
        ICON_FLAG='/resource:'
        LOG4NET_DIR="$EXTERNAL_DIR/log4net/bin/mono/1.0/release"
        NUNIT="$TOOLDIR/NUnit/bin/nunit-console.exe"
        NUNIT_LIBPATH="$TOOLDIR/NUnit/bin"
        NUNIT_FLAGS='/nologo'
        NUNITFLAGS_DEBUG=''
		MONO=mint
        NUNITFLAGS_OPTIMIZE=''
        PLATFORM_SUBDIRS=$DARWIN_SUBDIRS
        REPORT_DIR='$(DESTDIR)$(bindir)'
        REPORT_EXE='mint $(top_srcdir)/tools/Report/Report.exe'
        SEP='/'
        SQLITELIB="libsqlite.so"
        SQLITELIB_PATH="$EXTERNAL_DIR/sqlite/linux"
    ;;
    #
    # Linux-specific configuration
    #
    linux)
        #
        # Set variables
        #
        COMMON_CLEAN_FILES='.cstore .simias'
        ICON_EXT='.ico'
        ICON_FLAG='/resource:'
        LOG4NET_DIR="$EXTERNAL_DIR/log4net/bin/mono/1.0/release"
        NUNIT="$TOOLDIR/NUnit/bin/nunit-console.exe"
        NUNIT_LIBPATH="$TOOLDIR/NUnit/bin"
        NUNIT_FLAGS='/nologo'
        NUNITFLAGS_DEBUG=''
        NUNITFLAGS_OPTIMIZE=''
        PLATFORM_SUBDIRS=$LINUX_SUBDIRS
        REPORT_DIR='$(DESTDIR)$(bindir)'
        REPORT_EXE='mono $(top_srcdir)/tools/Report/Report.exe'
        SEP='/'
        SQLITELIB="libsqlite.so"
        SQLITELIB_PATH="$EXTERNAL_DIR/sqlite/linux"
    ;;
    #
    # Windows-specific configuration
    #
    windows)
        COMMON_CLEAN_FILES='*.suo */*.suo *.csproj.user */*.csproj.user bin obj */bin */obj *.xml */*.xml *.pdb */*.pdb .cstore .simias'
        ICON_EXT='.ico'
        ICON_FLAG='/win32icon:'
        LOG4NET_DIR="$EXTERNAL_DIR/log4net/bin/net/1.0/release"
        NUNIT="$TOOLDIR/NUnit/bin/nunit-console.exe"
        NUNIT_LIBPATH='$(shell cygpath --windows "$TOOLDIR/NUnit/bin")'
        NUNIT_FLAGS='/nologo'
        NUNITFLAGS_DEBUG=''
        NUNITFLAGS_OPTIMIZE=''
        PLATFORM_SUBDIRS=$WINDOWS_SUBDIRS
        REPORT_DIR='$(shell cygpath --windows $(DESTDIR)$(bindir) | sed ''s,\\\\,/,g'')'
        REPORT_EXE='$(top_srcdir)/tools/Report/Report.exe'
        SEP='$(EMPTY)\\$(EMPTY)'
        SQLITELIB="sqlite.dll"
        SQLITELIB_PATH="$EXTERNAL_DIR/sqlite/w32"
    ;;
esac
AC_SUBST(COMMON_CLEAN_FILES)
AC_SUBST(ICON_EXT)
AC_SUBST(ICON_FLAG)
AC_SUBST(LOG4NET_DIR)
AC_SUBST(NUNIT)
AC_SUBST(NUNIT_FLAGS)
AC_SUBST(NUNIT_LIBPATH)
AC_SUBST(PLATFORM_SUBDIRS)
AC_SUBST(REPORT_DIR)
AC_SUBST(REPORT_EXE)
AC_SUBST(SEP)
AC_SUBST(SQLITELIB)
AC_SUBST(SQLITELIB_PATH)

#
# Run standard macros
#
AM_PROG_CC_STDC
AC_PROG_INSTALL
AC_HEADER_STDC
#AM_PROG_LIBTOOL

#
# Handle --with-ndoc-path
#
AC_ARG_WITH(ndoc-path, [
  --with-ndoc-path=PATH        path to dir that contains NDocConsole.exe [[NONE]]],
    [ NDOC_PATH="$withval" ],
    [ NDOC_PATH=NONE ]
    )
if test "$NDOC_PATH" = "NONE"; then
    NDOC_PATH=''
else
    if test `basename "$NDOC_PATH"` = "NDocConsole.exe"; then
        NDOC_PATH=`dirname "$NDOC_PATH"`
    fi
    test -d "$NDOC_PATH" || AC_MSG_ERROR([$NDOC_PATH does not exist])
    NDOC_PATH=`cd "$NDOC_PATH" && pwd`
fi
if test -n "$NDOC_PATH"; then
    if test -e "$NDOC_PATH/NDocConsole.exe"; then
        NDOC_CMD="$NDOC_PATH/NDocConsole.exe"
    fi
    test -e "$NDOC_CMD" || AC_MSG_ERROR([NDocConsole.exe not found in $NDOC_PATH])
fi
echo NDOC_CMD=$NDOC_CMD
AC_SUBST(NDOC_CMD)
AM_CONDITIONAL(NDOC, test -n "$NDOC_CMD")

#
# Handle --enable-debug
#
AC_ARG_ENABLE(debug, [
  --enable-debug          configure the Makefiles to build in DEBUG mode],
    [case "${enableval}" in
        yes) enable_debug=true ;;
        no)  enable_debug=false ;;
        *) AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
    esac],[enable_debug=false])
AM_CONDITIONAL(DEBUG, test x$enable_debug = xtrue)
if test "$enable_debug" = "true"
then
  # Build debug version.
  CFLAGS="$CFLAGS_DEBUG $CFLAGS"
  CSCFLAGS="$CSCFLAGS_DEBUG $CSCFLAGS"
  CXXFLAGS="$CXXFLAGS_DEBUG $CXXFLAGS"
  DEVENV_CONFIGURATION=Debug
  MONO=$MONO_DEBUG
  NUNIT_FLAGS="$NUNITFLAGS_DEBUG $NUNIT_FLAGS"
else
  # Build optimized version.
  CFLAGS="$CFLAGS_OPTIMIZE $CFLAGS"
  CSCFLAGS="$CSCFLAGS_OPTIMIZE $CSCFLAGS"
  CXXFLAGS="$CXXFLAGS_OPTIMIZE $CXXFLAGS"
  DEVENV_CONFIGURATION=Release
  NUNIT_FLAGS="$NUNITFLAGS_OPTIMIZE $NUNIT_FLAGS"
fi
AC_SUBST(CSCFLAGS)
AC_SUBST(DEVENV_CONFIGURATION)

#
# Configure PKG_CONFIG
#
AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
if test "x$PKG_CONFIG" = "xno"; then
	AC_MSG_ERROR([You need to install pkg-config])
fi
case $SIMIAS_OS in
    linux)
        GTK_SHARP_REQUIRED_VERSION=0.10
        PKG_CHECK_MODULES(BASE_DEPENDENCIES, gtk-sharp >= $GTK_SHARP_REQUIRED_VERSION)
    ;;
esac

#
# Configure files
#
AC_OUTPUT([
Makefile
src/Makefile
src/Common/Makefile
src/CommonGtkWindows/Makefile
src/CommonFormWindows/Makefile
src/CollectionEvents/Makefile
src/UserService/Makefile
src/Storage/Makefile
src/StoreProvider/Makefile
src/FsProvider/Makefile
src/SqliteProvider/Makefile
src/CollectionStore/Makefile
src/FsWatcher/Makefile
src/RemotingSniffer/Makefile
src/SyncCommon/Makefile
src/StoreWatcher/Makefile
src/Location/Makefile
src/Agent/Makefile
src/Sync/Makefile
src/SyncManager/Makefile
src/SyncLogicLite/Makefile
src/SyncTools/Makefile
src/AddressBookCmd/Makefile
src/AddressBook/Makefile
src/Smtp/Makefile
src/Invitation/Makefile
src/MiniShare/Makefile
src/AcceptanceTests/Makefile
package/Makefile
package/windows/Makefile
package/linux/Makefile
package/darwin/Makefile
package/linux/simias.spec
doc/Makefile
])
